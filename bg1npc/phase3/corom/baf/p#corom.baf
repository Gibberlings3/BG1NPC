/* Romance Match */
IF %BGT_VAR%
Global("P#CoranMatch","GLOBAL",0)
Gender(Player1,FEMALE)
!Global("X#AjantisRomanceActive","GLOBAL",2)
!Global("X#XaRomanceActive","GLOBAL",2)
!Global("P#CoranRomanceInactive","GLOBAL",1)
!InParty("safana")
!Alignment(Player1,LAWFUL_EVIL)
OR(3)
Race(Player1,HUMAN)
Race(Player1,ELF)
Race(Player1,HALF_ELF)
ReputationGT(Player1,10)
CheckStatGT(Player1,12,CHR)
THEN
RESPONSE #100
SetGlobal("P#CoranMatch","GLOBAL",1)
END

/* NPC Reactions */
IF %BGT_VAR%
InParty(Myself)
Global("P#CoranMatch","GLOBAL",1)
Global("P#CoranRomancePath","GLOBAL",0)
Global("P#CoRomReaction","GLOBAL",0)
THEN
RESPONSE #100
RealSetGlobalTimer("P#CRoReactionTime","GLOBAL",500)
SetGlobal("P#CoRomReaction","GLOBAL",1)
END

/* Set Romance Break if Ajantis or Xan are serious */
IF %BGT_VAR%
Global("P#CoranMatch","GLOBAL",1)
Global("X#AjantisRomanceActive","GLOBAL",2)
!Global("P#CoranRomanceInactive","GLOBAL",1)
InParty(Myself)
InParty("ajantis")
THEN
RESPONSE #100
SetGlobal("P#CoranRomCutByOther","GLOBAL",1)
SetGlobal("P#CoranRomanceInactive","GLOBAL",1)
END

IF %BGT_VAR%
!Global("P#CoranRomanceInactive","GLOBAL",1)
Global("P#CoranMatch","GLOBAL",1)
!Global("X#XARomanceInactive","GLOBAL",1)
Global("X#XARomanceActive","GLOBAL",2)
InParty(Myself)
InParty("xan")
THEN
RESPONSE #100
SetGlobal("P#CoranRomCutByOther","GLOBAL",1)
SetGlobal("P#CoranRomanceInactive","GLOBAL",1)
END

/* chaotic_good */
IF %BGT_VAR%
Global("P#CoranMatch","GLOBAL",1)
Global("P#CoranRomancePath","GLOBAL",0)
Global("P#CGLoveTalk","GLOBAL",0)
InParty(Myself)
THEN
RESPONSE #100
RealSetGlobalTimer("P#CGLoveTalkTime","GLOBAL",5)
SetGlobal("P#CGLoveTalk","GLOBAL",1)
SetGlobal("P#CoranRomancePath","GLOBAL",1)
END

IF %BGT_VAR%
RealGlobalTimerExpired("P#CGLoveTalkTime","GLOBAL")
InParty(Myself)
!See([ENEMY])
!StateCheck(Player1,CD_STATE_NOTVALID)
!StateCheck(Myself,CD_STATE_NOTVALID)
See(Player1)
CombatCounter(0)
ReputationGT(Player1,10)
OR(2)
Global("P#CoranRomancePath","GLOBAL",1)
Global("P#CoranRomancePath","GLOBAL",6)
!Global("P#CoranRomanceInactive","GLOBAL",1)
OR(9)
Global("P#CGLoveTalk","GLOBAL",1)
Global("P#CGLoveTalk","GLOBAL",3)
Global("P#CGLoveTalk","GLOBAL",7)
Global("P#CGLoveTalk","GLOBAL",11)
Global("P#CGLoveTalk","GLOBAL",17)
Global("P#CGLoveTalk","GLOBAL",19)
Global("P#CGLoveTalk","GLOBAL",21)
Global("P#CGLoveTalk","GLOBAL",23)
Global("P#CGLoveTalk","GLOBAL",31)
!InParty("safana")
!AreaType(DUNGEON)
THEN
RESPONSE #100
IncrementGlobal("P#CGLoveTalk","GLOBAL",1)
END

IF %BGT_VAR%
RealGlobalTimerExpired("P#CGLoveTalkTime","GLOBAL")
InParty(Myself)
!StateCheck(Player1,CD_STATE_NOTVALID)
!StateCheck(Myself,CD_STATE_NOTVALID)
OR(16)
Global("P#CGLoveTalk","GLOBAL",2)
Global("P#CGLoveTalk","GLOBAL",4)
Global("P#CGLoveTalk","GLOBAL",6)
Global("P#CGLoveTalk","GLOBAL",8)
Global("P#CGLoveTalk","GLOBAL",10)
Global("P#CGLoveTalk","GLOBAL",12)
Global("P#CGLoveTalk","GLOBAL",14)
Global("P#CGLoveTalk","GLOBAL",16)
Global("P#CGLoveTalk","GLOBAL",18)
Global("P#CGLoveTalk","GLOBAL",20)
Global("P#CGLoveTalk","GLOBAL",22)
Global("P#CGLoveTalk","GLOBAL",24)
Global("P#CGLoveTalk","GLOBAL",26)
Global("P#CGLoveTalk","GLOBAL",28)
Global("P#CGLoveTalk","GLOBAL",30)
Global("P#CGLoveTalk","GLOBAL",32)
THEN
RESPONSE #100
PlaySong(0)
PlaySound("corang")
StartDialogueNoSet(Player1)
END

IF %BGT_VAR%
InParty(Myself)
CombatCounter(0)
!See([ENEMY])
!StateCheck(Player1,CD_STATE_NOTVALID)
!StateCheck(Myself,CD_STATE_NOTVALID)
See(Player1)
RealGlobalTimerExpired("P#CGLoveTalkTime","GLOBAL")
OR(2)
Global("P#CoranRomancePath","GLOBAL",1)
Global("P#CoranRomancePath","GLOBAL",6)
!Global("P#CoranRomanceInactive","GLOBAL",1)
Global("P#CGLoveTalk","GLOBAL",5)
ReputationGT(Player1,10)
!InParty("safana")
PartyRested()
!AreaType(DUNGEON)
THEN
RESPONSE #100
IncrementGlobal("P#CGLoveTalk","GLOBAL",1)
END

IF %BGT_VAR%
InParty(Myself)
CombatCounter(0)
!See([ENEMY])
!StateCheck(Player1,CD_STATE_NOTVALID)
!StateCheck(Myself,CD_STATE_NOTVALID)
See(Player1)
OR(2)
Global("P#CoranRomancePath","GLOBAL",1)
Global("P#CoranRomancePath","GLOBAL",6)
!Global("P#CoranRomanceInactive","GLOBAL",1)
Global("P#CoOcean","GLOBAL",1)
Global("P#CGLoveTalk","GLOBAL",29)
ReputationGT(Player1,10)
!InParty("safana")
!AreaType(DUNGEON)
THEN
RESPONSE #100
IncrementGlobal("P#CGLoveTalk","GLOBAL",1)
END

/* bored */
IF %BGT_VAR%
InParty(Myself)
Global("P#CoranMatch","GLOBAL",1)
Global("P#CoranRomancePath","GLOBAL",2)
Global("P#CBoredTalk","GLOBAL",0)
THEN
RESPONSE #100
RealSetGlobalTimer("P#CBoredTalkTime","GLOBAL",5)
SetGlobal("P#CBoredTalk","GLOBAL",1)
END

IF %BGT_VAR%
RealGlobalTimerExpired("P#CBoredTalkTime","GLOBAL")
InParty(Myself)
CombatCounter(0)
!See([ENEMY])
!StateCheck(Player1,CD_STATE_NOTVALID)
!StateCheck(Myself,CD_STATE_NOTVALID)
See(Player1)
Global("P#CoranRomancePath","GLOBAL",2)
!Global("P#CoranRomanceInactive","GLOBAL",1)
!InParty("safana")
Global("P#CBoredTalk","GLOBAL",1)
ReputationGT(Player1,10)
OR(20)
AreaCheck("%Beregost_FeldepostsInn_L1%")
AreaCheck("%Beregost_JovialJuggler_L1%")
AreaCheck("%Beregost_RedSheaf_L1%")
AreaCheck("%Beregost_BurningWizard_L1%")
AreaCheck("%Nashkel_Inn%")
AreaCheck("%EBaldursGate_KeexieTavern_L1%")
AreaCheck("%EBaldursGate_ElfsongTavern_L1%")
AreaCheck("%EBaldursGate_Inn_L1%")
AreaCheck("%SEBaldursGate_Inn_L1%")
AreaCheck("%SEBaldursGate_Blade&Stars_L1%")
AreaCheck("%BaldursGateDocks_JopalinsTavern%")
AreaCheck("%SWBaldursGate_Tavern_L1%")
AreaCheck("%SWBaldursGate_YeOldeInn_L1%")
AreaCheck("%NWBaldursGate_Helm&Cloak_L1%")
AreaCheck("%NWBaldursGate_Tavern_L1%")
AreaCheck("%NBaldursGate_ThreeOldKegs_L1%")
AreaCheck("%NEBaldursGate_BlushingMermaid_L1%")
AreaCheck("%NEBaldursGate_SplurgingSturgeon_L2%")
AreaCheck("%FriendlyArmInn_L1%")
AreaCheck("%NEBaldursGate_SplurgingSturgeon_L1%")
THEN
RESPONSE #100
IncrementGlobal("P#CBoredTalk","GLOBAL",1)
END

IF %BGT_VAR%
Global("P#CBoredTalk","GLOBAL",2)
InParty(Myself)
!StateCheck(Player1,CD_STATE_NOTVALID)
!StateCheck(Myself,CD_STATE_NOTVALID)
THEN
RESPONSE #100
PlaySong(0)
PlaySound("coranf")
StartDialogueNoSet(Player1)
END

IF %BGT_VAR%
RealGlobalTimerExpired("P#CBoredTalkTime","GLOBAL")
InParty(Myself)
CombatCounter(0)
!See([ENEMY])
!StateCheck(Player1,CD_STATE_NOTVALID)
!StateCheck(Myself,CD_STATE_NOTVALID)
See(Player1)
Global("P#CoranRomancePath","GLOBAL",2)
!Global("P#CoranRomanceInactive","GLOBAL",1)
Global("P#CBoredTalk","GLOBAL",5)
ReputationGT(Player1,10)
TimeOfDay(MORNING)
!AreaType(DUNGEON)
THEN
RESPONSE #100
IncrementGlobal("P#CBoredTalk","GLOBAL",1)
END

IF %BGT_VAR%
Global("P#CBoredTalk","GLOBAL",6)
InParty(Myself)
!StateCheck(Player1,CD_STATE_NOTVALID)
!StateCheck(Myself,CD_STATE_NOTVALID)
THEN
RESPONSE #100
PlaySong(0)
PlaySound("coranf")
StartDialogueNoSet(Player1)
END

/* Neutral_1 */
IF %BGT_VAR%
InParty(Myself)
Global("P#CoranMatch","GLOBAL",1)
Global("P#CoranRomancePath","GLOBAL",3)
Global("P#CNeutralTalk","GLOBAL",0)
THEN
RESPONSE #100
RealSetGlobalTimer("P#CNeutralTalkTime","GLOBAL",5)
SetGlobal("P#CNeutralTalk","GLOBAL",1)
END

IF %BGT_VAR%
InParty(Myself)
CombatCounter(0)
!See([ENEMY])
!StateCheck(Player1,CD_STATE_NOTVALID)
!StateCheck(Myself,CD_STATE_NOTVALID)
See(Player1)
RealGlobalTimerExpired("P#CNeutralTalkTime","GLOBAL")
Global("P#CoranRomancePath","GLOBAL",3)
!Global("P#CoranRomanceInactive","GLOBAL",1)
OR(2)
Global("P#CNeutralTalk","GLOBAL",1)
Global("P#CNeutralTalk","GLOBAL",9)
!AreaType(DUNGEON)
THEN
RESPONSE #100
IncrementGlobal("P#CNeutralTalk","GLOBAL",1)
END

IF %BGT_VAR%
OR(2)
Global("P#CNeutralTalk","GLOBAL",2)
Global("P#CNeutralTalk","GLOBAL",10)
InParty(Myself)
!StateCheck(Player1,CD_STATE_NOTVALID)
!StateCheck(Myself,CD_STATE_NOTVALID)
THEN
RESPONSE #100
PlaySong(0)
PlaySound("corann")
StartDialogueNoSet(Player1)
END

IF %BGT_VAR%
InParty(Myself)
CombatCounter(0)
!See([ENEMY])
RealGlobalTimerExpired("P#CNeutralTalkTime","GLOBAL")
Global("P#CoranRomancePath","GLOBAL",3)
!Global("P#CoranRomanceInactive","GLOBAL",1)
Global("P#CoranMatch","GLOBAL",1)
GlobalLT("P#CBoredTalk","GLOBAL",2)
Global("P#CNeutralTalk","GLOBAL",5)
OR(20)
AreaCheck("%Beregost_FeldepostsInn_L1%")
AreaCheck("%Beregost_JovialJuggler_L1%")
AreaCheck("%Beregost_RedSheaf_L1%")
AreaCheck("%Beregost_BurningWizard_L1%")
AreaCheck("%Nashkel_Inn%")
AreaCheck("%EBaldursGate_KeexieTavern_L1%")
AreaCheck("%EBaldursGate_ElfsongTavern_L1%")
AreaCheck("%EBaldursGate_Inn_L1%")
AreaCheck("%SEBaldursGate_Inn_L1%")
AreaCheck("%SEBaldursGate_Blade&Stars_L1%")
AreaCheck("%BaldursGateDocks_JopalinsTavern%")
AreaCheck("%SWBaldursGate_Tavern_L1%")
AreaCheck("%SWBaldursGate_YeOldeInn_L1%")
AreaCheck("%NWBaldursGate_Helm&Cloak_L1%")
AreaCheck("%NWBaldursGate_Tavern_L1%")
AreaCheck("%NBaldursGate_ThreeOldKegs_L1%")
AreaCheck("%NEBaldursGate_BlushingMermaid_L1%")
AreaCheck("%NEBaldursGate_SplurgingSturgeon_L2%")
AreaCheck("%FriendlyArmInn_L1%")
AreaCheck("%NEBaldursGate_SplurgingSturgeon_L1%")
THEN
RESPONSE #100
IncrementGlobal("P#CNeutralTalk","GLOBAL",1)
END

IF %BGT_VAR%
Global("P#CNeutralTalk","GLOBAL",6)
InParty(Myself)
!StateCheck(Player1,CD_STATE_NOTVALID)
!StateCheck(Myself,CD_STATE_NOTVALID)
THEN
RESPONSE #100
PlaySong(0)
PlaySound("corann")
StartDialogueNoSet(Player1)
END

IF %BGT_VAR%
Global("P#Co3Healer","GLOBAL",1)
InParty(Myself)
CombatCounter(0)
!See([ENEMY])
!StateCheck(Player1,CD_STATE_NOTVALID)
!StateCheck(Myself,CD_STATE_NOTVALID)
Global("P#CoranRomancePath","GLOBAL",3)
!Global("P#CoranRomanceInactive","GLOBAL",1)
THEN
RESPONSE #100
IncrementGlobal("P#Co3Healer","GLOBAL",1)
END

IF %BGT_VAR%
InParty(Myself)
!StateCheck(Player1,CD_STATE_NOTVALID)
!StateCheck(Myself,CD_STATE_NOTVALID)
Global("P#Co3Healer","GLOBAL",2)
THEN
RESPONSE #100
PlaySong(0)
PlaySound("corann")
StartDialogueNoSet(Player1)
END

IF %BGT_VAR%
InParty(Myself)
Global("P#CoranRomancePath","GLOBAL",3)
!Global("P#CoranRomanceInactive","GLOBAL",1)
Global("P#Co3Marriage","GLOBAL",1)
THEN
RESPONSE #100
IncrementGlobal("P#Co3Marriage","GLOBAL",1)
END

IF %BGT_VAR%
Global("P#Co3Marriage","GLOBAL",2)
InParty(Myself)
!StateCheck(Player1,CD_STATE_NOTVALID)
!StateCheck(Myself,CD_STATE_NOTVALID)
THEN
RESPONSE #100
PlaySong(0)
PlaySound("corann")
StartDialogueNoSet(Player1)
END

/* boredneutral */
IF %BGT_VAR%
InParty(Myself)
!Global("P#CoranRomanceInactive","GLOBAL",1)
Global("P#CoranMatch","GLOBAL",1)
Global("P#CoranRomancePath","GLOBAL",4)
GlobalLT("P#CBoredTalk","GLOBAL",5)
Global("P#CNBoredTalk","GLOBAL",0)
THEN
RESPONSE #100
RealSetGlobalTimer("P#CNBoredTalkTime","GLOBAL",5)
SetGlobal("P#CNBoredTalk","GLOBAL",1)
END

IF %BGT_VAR%
RealGlobalTimerExpired("P#CNBoredTalkTime","GLOBAL")
InParty(Myself)
CombatCounter(0)
!See([ENEMY])
Global("P#CoranRomancePath","GLOBAL",4)
!Global("P#CoranRomanceInactive","GLOBAL",1)
Global("P#CNBoredTalk","GLOBAL",1)
TimeOfDay(MORNING)
!AreaType(DUNGEON)
THEN
RESPONSE #100
IncrementGlobal("P#CNBoredTalk","GLOBAL",1)
END

IF %BGT_VAR%
Global("P#CNBoredTalk","GLOBAL",2)
InParty(Myself)
!StateCheck(Player1,CD_STATE_NOTVALID)
!StateCheck(Myself,CD_STATE_NOTVALID)
THEN
RESPONSE #100
PlaySong(0)
PlaySound("coranf")
StartDialogueNoSet(Player1)
END

/* babytalks */
IF %BGT_VAR%
InParty(Myself)
Global("P#CoranMatch","GLOBAL",1)
!Global("P#CoranRomanceInactive", "GLOBAL", 1)
Global("P#CGBabyTalk","GLOBAL",0)
Global("P#Briel_Stay","GLOBAL",4)
THEN
RESPONSE #100
RealSetGlobalTimer("P#CGBabyTalkTime","GLOBAL",5)
SetGlobal("P#CGBabyTalk","GLOBAL",1)
SetGlobal("P#CoranRomancePath","GLOBAL",6)
END

IF %BGT_VAR%
RealGlobalTimerExpired("P#CGBabyTalkTime","GLOBAL")
InParty(Myself)
!InParty("safana")
!AreaType(DUNGEON)
CombatCounter(0)
!See([ENEMY])
!StateCheck(Player1,CD_STATE_NOTVALID)
!StateCheck(Myself,CD_STATE_NOTVALID)
See(Player1)
ReputationGT(Player1,10)
Global("P#CoranRomancePath","GLOBAL",6)
!Global("P#CoranRomanceInactive","GLOBAL",1)
GlobalGT("P#CGLoveTalk","GLOBAL",32)
OR(3)
Global("P#CGBabyTalk","GLOBAL",1)
Global("P#CGBabyTalk","GLOBAL",3)
Global("P#CGBabyTalk","GLOBAL",5)
OR(50)
AreaCheck("%NWBaldursGate%")
AreaCheck("%WBaldursGate%")
AreaCheck("%SWBaldursGate%")
AreaCheck("%NBaldursGate%")
AreaCheck("%CentralBaldursGate%")
AreaCheck("%BaldursGateDocks%")
AreaCheck("%NEBaldursGate%")
AreaCheck("%EBaldursGate%")
AreaCheck("%SEBaldursGate%")
AreaCheck("%Farmlands%")
AreaCheck("%UlgothsBeard%")
AreaCheck("%WyrmsCrossing%")
AreaCheck("%FishingVillage%")
AreaCheck("%BanditCamp%")
AreaCheck("%CloakwoodWyverns%")
AreaCheck("%CloakwoodMines%")
AreaCheck("%CloakwoodDruids%")
AreaCheck("%FriendlyArmInn%")
AreaCheck("%Peldvale%")
AreaCheck("%CloakwoodNest%")
AreaCheck("%CloakwoodLodge%")
AreaCheck("%Larswood%")
AreaCheck("%SpiderWood%")
AreaCheck("%LionsWay%")
AreaCheck("%CoastWay%")
AreaCheck("%ShipwrecksCoast%")
AreaCheck("%HighHedge%")
AreaCheck("%Beregost%")
AreaCheck("%Temple%")
AreaCheck("%MutaminsGarden%")
AreaCheck("%Lighthouse%")
AreaCheck("%RedCanyons%")
AreaCheck("%SouthBeregostRoad%")
AreaCheck("%Ulcaster%")
AreaCheck("%Gullykin%")
AreaCheck("%ArchaeologicalSite%")
AreaCheck("%FishermansLake%")
AreaCheck("%NorthNashkelRoad%")
AreaCheck("%LonelyPeaks%")
AreaCheck("%FirewineBridge%")
AreaCheck("%XvartVillage%")
AreaCheck("%Nashkel%")
AreaCheck("%NashkelCarnival%")
AreaCheck("%ValleyoftheTombs%")
AreaCheck("%BearRiver%")
AreaCheck("%GnollStronghold%")
AreaCheck("%DryadFalls%")
AreaCheck("%FireLeafForest%")
AreaCheck("%NashkelMines%")
AreaCheck("%GibberlingMountains%")
THEN
RESPONSE #100
IncrementGlobal("P#CGBabyTalk","GLOBAL",1)
END

IF %BGT_VAR%
RealGlobalTimerExpired("P#CGBabyTalkTime","GLOBAL")
InParty(Myself)
!InParty("safana")
!AreaType(DUNGEON)
CombatCounter(0)
!See([ENEMY])
!StateCheck(Player1,CD_STATE_NOTVALID)
!StateCheck(Myself,CD_STATE_NOTVALID)
See(Player1)
ReputationGT(Player1,10)
Global("P#CoranRomancePath","GLOBAL",6)
!Global("P#CoranRomanceInactive","GLOBAL",1)
GlobalGT("P#CGLoveTalk","GLOBAL",32)
OR(3)
Global("P#CGBabyTalk","GLOBAL",1)
Global("P#CGBabyTalk","GLOBAL",3)
Global("P#CGBabyTalk","GLOBAL",5)
THEN
RESPONSE #100
RealSetGlobalTimer("P#CGBabyTalkTime","GLOBAL",FIVE_MINUTES)
END

IF %BGT_VAR%
RealGlobalTimerExpired("P#CGBabyTalkTime","GLOBAL")
InParty(Myself)
!StateCheck(Player1,CD_STATE_NOTVALID)
!StateCheck(Myself,CD_STATE_NOTVALID)
OR(3)
Global("P#CGBabyTalk","GLOBAL",2)
Global("P#CGBabyTalk","GLOBAL",4)
Global("P#CGBabyTalk","GLOBAL",6)
THEN
RESPONSE #100
PlaySong(0)
PlaySound("coranb")
StartDialogueNoSet(Player1)
END

/* coran-moves */
IF %BGT_VAR%
!Global("P#CoranRomancePath","GLOBAL",7)
Global("P#CoranMoves","GLOBAL",1)
THEN
RESPONSE #100
SetGlobal("P#CoranRomancePath","GLOBAL",7)
SetGlobal("P#CoranHasBaby","GLOBAL",2)
RealSetGlobalTimer("P#CoranReturn","GLOBAL",5)
TakePartyItem("X#CBABY")
TakePartyItem("X#CBOOK")
TakePartyItem("X#CMILK")
DestroyItem("X#CBABY")
DestroyItem("X#CBOOK")
DestroyItem("X#CMILK")
EscapeAreaMove("%NEBaldursGate_SplurgingSturgeon_L1%",368,402,2)
END

/* Hanali Flirtz */
IF %BGT_VAR%
!Global("P#CoranRomanceInactive", "GLOBAL",1)
Global("P#CoranMatch","GLOBAL",1)
GlobalGT("P#CoranLover","GLOBAL",0)
Global("X#CoranHanaliRune","GLOBAL",2)
InParty(Myself)
THEN
RESPONSE #100
IncrementGlobal("X#CoranHanaliRune","GLOBAL",1)
END

IF %BGT_VAR%
Global("X#CoranHanaliRune","GLOBAL",3)
InParty(Myself)
!StateCheck(Player1,CD_STATE_NOTVALID)
!StateCheck(Myself,CD_STATE_NOTVALID)
THEN
RESPONSE #100
PlaySong(0)
PlaySound("corang")
StartDialogueNoSet(Player1)
END

/* Coran Gives up after Longing talk */
IF %BGT_VAR%
Global("P#CoranMatch","GLOBAL",1)
GlobalGT("P#CoranLover","GLOBAL",0)
GlobalTimerExpired("X#CoranLonging","GLOBAL")
Global("X#CoranHanaliRune","GLOBAL",10)
InParty(Myself)
THEN
RESPONSE #100
IncrementGlobal("X#CoranHanaliRune","GLOBAL",1)
END

IF %BGT_VAR%
Global("X#CoranHanaliRune","GLOBAL",11)
InParty(Myself)
!StateCheck(Player1,CD_STATE_NOTVALID)
!StateCheck(Myself,CD_STATE_NOTVALID)
THEN
RESPONSE #100
PlaySong(0)
PlaySound("corang")
StartDialogueNoSet(Player1)
END