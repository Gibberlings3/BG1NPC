IF %BGT_VAR%
Global("X#CoranSuccubus","GLOBAL",3)
InParty("coran")
Global("X#AmeliaExist","%FirewineBridge%",0)
THEN
RESPONSE #100
SetGlobal("X#AmeliaExist","%FirewineBridge%",1)
CreateCreature("X#AMELIA",[4249.2100],3)
END

IF %BGT_VAR%
Dead("X#amelia")
Exists("natan")
!Allegiance("natan",NEUTRAL)
THEN
RESPONSE #100
ChangeEnemyAlly("natan",NEUTRAL)
END

IF %BGT_VAR%
Dead("X#amelia")
Exists("natan")
!Dead("natan")
GlobalLT("X#CoranSuccubus","GLOBAL",8)
THEN
RESPONSE #100
SetGlobal("X#CoranSuccubus","GLOBAL",20)
ActionOverride("natan",DestroySelf())
END

IF %BGT_VAR%
Dead("X#amelia")
Exists("natan")
!Dead("natan")
OR(4)
Global("X#CoranSuccubus","GLOBAL",9)
Global("X#CoranSuccubus","GLOBAL",10)
Global("X#CoranSuccubus","GLOBAL",11)
Global("X#CoranSuccubus","GLOBAL",14)
THEN
RESPONSE #100
SetGlobal("X#CoranSuccubus","GLOBAL",13)
END

IF %BGT_VAR%
Dead("X#amelia")
!Allegiance("coran",ALLY)
!Global("X#CoranCharmed","GLOBAL",2)
THEN
RESPONSE #100
ChangeEnemyAlly("coran",ALLY)
SetGlobal("X#CoranCharmed","GLOBAL",2)
END

IF %BGT_VAR%
OR(2)
Dead("X#amelia")
Global("X#CoranSuccubus","GLOBAL",16)
!InParty("coran")
Global("X#CoranCharmed","GLOBAL",1)
THEN
RESPONSE #100
SetGlobal("X#CoranCharmed","GLOBAL",2)
FadeToColor([20.0],0)
MoveGlobalObject("coran",Player1)
ActionOverride("coran",JoinParty())
FadeFromColor([20.0],0)
END
