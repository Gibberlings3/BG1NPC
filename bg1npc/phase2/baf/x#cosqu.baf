/* Bringing in Natan */
IF %BGT_VAR%
AreaCheck("%FirewineRuins%")
Global("X#NatanExists","GLOBAL",0)
GlobalGT("X#CoranSuccubus","GLOBAL",0)
THEN
RESPONSE #100
SetGlobal("X#NatanExists","GLOBAL",1)
CreateCreature("X#NATAN",[1971.1733],3)
END

/* Entered Firewine Ruins */
IF %BGT_VAR%
Global("X#CoranSuccubus","GLOBAL",0)
AreaCheck("%FirewineBridge%")
THEN
RESPONSE #100
SetGlobal("X#CoranSuccubus","GLOBAL",1)
RealSetGlobalTimer("X#SeeSuccubusTime","GLOBAL",30)
END

IF %BGT_VAR%
Global("X#CoranSuccubus","GLOBAL",1)
AreaCheck("%FirewineBridge%")
RealGlobalTimerExpired("X#SeeSuccubusTime","GLOBAL")
InParty(Myself)
!StateCheck(Myself,CD_STATE_NOTVALID)
InMyArea(Player1)
!StateCheck(Player1,CD_STATE_NOTVALID)
CombatCounter(0)
!See([ENEMY])
THEN
RESPONSE #100
SetGlobal("X#CoranSuccubus","GLOBAL",2)
END

/* Initiate */
IF %BGT_VAR%
Global("X#CoranSuccubus","GLOBAL",2)
InParty(Myself)
!StateCheck(Myself,CD_STATE_NOTVALID)
InMyArea(Player1)
!StateCheck(Player1,CD_STATE_NOTVALID)
CombatCounter(0)
!See([ENEMY])
THEN
RESPONSE #100
StartDialogueNoSet(Player1)
END

IF %BGT_VAR%
AreaCheck("%Temple%")
Global("X#RashelExists","GLOBAL",0)
InParty(Myself)
!StateCheck(Myself,CD_STATE_NOTVALID)
InMyArea(Player1)
!StateCheck(Player1,CD_STATE_NOTVALID)
CombatCounter(0)
!See([ENEMY])
THEN
RESPONSE #100
SetGlobal("X#RashelExists","GLOBAL",1)
CreateCreature("X#RASHEL",[1059.1089],3)
END

IF %BGT_VAR%
Global("X#CoranCharmedRun","GLOBAL",0)
Global("X#CoranSuccubus","GLOBAL",11)
!Dead("X#amelia")
RealGlobalTimerExpired("X#CoranResistTimer","GLOBAL")
InParty(Myself)
!StateCheck(Myself,CD_STATE_NOTVALID)
InMyArea(Player1)
!StateCheck(Player1,CD_STATE_NOTVALID)
CombatCounter(0)
!See([ENEMY])
THEN
RESPONSE #100
SetGlobal("X#CoranCharmedRun","GLOBAL",1)
END

IF %BGT_VAR%
Global("X#CoranCharmedRun","GLOBAL",1)
InParty(Myself)
!StateCheck(Myself,CD_STATE_NOTVALID)
InMyArea(Player1)
!StateCheck(Player1,CD_STATE_NOTVALID)
THEN
RESPONSE #100
StartDialogueNoSet(Player1)
END

