/* ALWAYS = Before each and any component, do this. */

/* SoD DLC check - Thanks, CamDawg! */
ACTION_IF FILE_EXISTS ~dlc/sod-dlc.zip~ THEN BEGIN
  	FAIL @5004
END

/* DW function libraries */

OUTER_SPRINT workspace "weidu_external/workspace"
MKDIR "%workspace%"

INCLUDE "bg1npc/lib/lib_interject.tpa"
INCLUDE "bg1npc/lib/alter_dlg.tpa"



/* Actions for BGEE and EET */
ACTION_IF GAME_IS ~bgee eet~ THEN BEGIN
	ACTION_IF GAME_IS ~bgee~ THEN BEGIN
		/*Tell the player it is using BG:EE stuff */
		PRINT @5000
  		INCLUDE ~bg1npc/lib/g3_bgee_cpmvars.tpa~
	END

	ACTION_IF GAME_IS ~eet~ THEN BEGIN
		INCLUDE  ~EET/other/cpmvars/eet_cpmvars.tpa~

	/* EET-SoD: PID should not block SoD dialogues. --- This is a hack and only valid because BG1NPC only adds to BG1! */
	OUTER_SPRINT ~BGT_VAR~ ~!GlobalGT("BD_PLOT","GLOBAL",0)~

	END


	// BG:EE tra conversion
	// Code borrowed from Edwin Romance and adjusted: HANDLE_CHARSETS in now included in WeiDU 237
	// For complete description check Edwin Romance and the WeiDU forum topic on HANDLE_CHARSETS

	// Only setup.tra is stated in LANGUAGE and needs to be reloaded after conversion
	ACTION_DEFINE_ARRAY x#reload BEGIN setup END
	ACTION_DEFINE_ARRAY x#noconvert BEGIN END

	LAF HANDLE_CHARSETS
		INT_VAR
			infer_charsets = 1
		STR_VAR
			tra_path = EVAL ~bg1npc/tra~
			noconvert_array = x#noconvert
			reload_array = x#reload
	END

END ELSE BEGIN /* Actions for Tutu or BGT */

	/* Tutu */
	ACTION_IF GAME_IS ~tutu tutu_totsc~ THEN BEGIN
		PRINT @1000
		INCLUDE ~bg1npc/lib/g3_tutu_cpmvars.tpa~

		APPEND ~GTIMES.IDS~  ~12 TWO_ROUNDS~    UNLESS ~TWO_ROUNDS~

	END ELSE BEGIN
	/* BGT */
    	ACTION_IF GAME_IS ~bgt~ THEN BEGIN
		PRINT @1001
		INCLUDE ~bg1npc/lib/g3_bgt_cpmvars.tpa~
    	END ELSE BEGIN 
    		/* Not a supported game */
    		FAIL @1002
		END
	END
END

/* include special variables */
INCLUDE ~bg1npc/lib/bg1npc_specvar.tpa~

/* include dream scripts */
INCLUDE ~bg1npc/lib/g3_dreamscript_cpmvars.tpa~

/*  alter fluid states */
INCLUDE ~bg1npc/lib/g3_fluid_cpmvars.tpa~

/* prep tras for sound references */
COPY ~bg1npc/TRA/%LANGUAGE%/BG1NPC_tmp.tra~ ~bg1npc/TRA/%LANGUAGE%/BG1NPC.tra~
	EVALUATE_BUFFER
LOAD_TRA ~bg1npc/TRA/%LANGUAGE%/BG1NPC.tra~
ACTION_IF GAME_IS ~bgee eet~ THEN BEGIN
	LOAD_TRA ~bg1npc/TRA/%LANGUAGE%/BGEE_ITEMDESC.tra~ /* Item descriptions for BGEE, i.e. without usability info */
END

INCLUDE ~bg1npc/lib/extra_regexp_vars.tpa~   /* allows us to regexp match tabs and newlines */
OUTER_SPRINT "whitespace" "[%tab% %lnl%%mnl%%wnl%]+" /* Defines regexp whitespaces */
INCLUDE ~bg1npc/lib/bg1npc_project_macros.tpa~   /* adds macros callable by each component */

/* prep function to be able to catch journal entries so they can be deleted (needed for BGT where the string numbers can vary from install to install). */

INCLUDE ~bg1npc/lib/func_fetch_journal_entries.tph~

/* read in extra 2 for different string numbers eetc/bg:ee */
OUTER_SET eet_200000 = GAME_IS ~eet~ ? 200000 : 0